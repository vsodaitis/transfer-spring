plugins {
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'java'
    id 'com.bmuschko.docker-remote-api' version'4.8.0'
}

apply plugin: 'io.spring.dependency-management'

import com.bmuschko.gradle.docker.tasks.image.*

group = 'com.vytenis'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

tasks.create('createDockerfile', Dockerfile) {
    destFile = project.file('build/docker/Dockerfile')
    from 'openjdk:8-jre-slim-stretch'
    copyFile jar.archiveFileName.get(), "/app/${archivesBaseName}.jar"
    entryPoint 'java'
    defaultCommand '-jar', "/app/${archivesBaseName}.jar"
}

tasks.create('syncAppArchive', Sync) {
    dependsOn bootJar
    from jar.archiveFile.get().asFile
    into createDockerfile.destFile.get().asFile.parentFile
}

createDockerfile.dependsOn syncAppArchive

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
